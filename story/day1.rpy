label day1:
    scene bg room
    
    show screen day_indicator("День 1")
    
    "Утро понедельника. Будильник звонит уже в третий раз."
    "Голова тяжелая. Началась сессия."
    
    $ player_name = renpy.input("Как меня зовут?", default="Алекс").strip()
    if not player_name:
        $ player_name = "Алекс"

    "Точно. Я [player_name]. И мне нужно выжить."

    scene bg hall # Placeholder
    "В университете шумно. Студенты сбились в кучки, лихорадочно листая конспекты."

    scene bg hallway
    "Ты видишь своих одногруппников. Кажется, все по-разному готовятся к экзаменам."
    show ayan at center
    
    "Аян выглядит спокойным, как удав. Он единственный, кто не паникует перед программированием."
    
    ayan "Привет, [player_name]. Готов к запуску?"
    
    menu:
        "Я всю ночь учил алгоритмы.":
            ayan "Правильный подход. Главное — понимание, а не зубрежка."
            $ ayan_relationship += 1
            $ helped_ayan = True
            "Аян кивает. Кажется, он уважает твою подготовку."
            
        "Честно говоря, руки трясутся.":
            ayan "Расслабься. Это просто код. Если есть логика, есть решение."
            "Он хлопает тебя по плечу."
            
    ayan "В первом задании часто ловят на синтаксисе. Смотри на отступы."
    "Это может пригодиться."
    
    hide ayan
    
    "Звонок. Пора в аудиторию."
    
    # EXAM 1: PROGRAMMING
    $ run_exam("programming")
    
    "Ты выходишь из аудитории, вытирая пот со лба."
    "Первый бой позади. Но расслабляться рано. Психология через час."
    
    show diana at right
    "Диана что-то активно обсуждает с группой."
    
    diana "Вы слышали? Он будет спрашивать про невербалику! Следите за жестами."
    
    menu:
        "Подслушать совет (Довериться Диане).":
            "Ты решаешь, что Диана знает, о чем говорит. Она всегда в курсе сплетен кафедры."
            $ trusted_diana = True
            $ diana_relationship += 1
            
        "Игнорировать и повторить теорию.":
            "Сплетни только сбивают. Лучше повторю Фрейда."
            $ trusted_diana = False

    hide diana
    
    "Ты заходишь в кабинет психологии. Преподаватель смотрит на тебя как на подопытного."
    
    # EXAM 2: PSYCHOLOGY
    $ run_exam("psychology")
    
    scene bg room
    "Вечер. Ты падаешь на кровать без сил."
    "Два экзамена. Осталось четыре."
    "Нужно поспать."
    
    jump day2
